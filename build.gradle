plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id("com.github.ben-manes.versions") version "0.51.0"
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

configurations {
    examplesImplementation.extendsFrom testImplementation
}

sourceSets {
    examples {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/examples/java')
        }
        resources.srcDir file('src/examples/resources')
    }
}

dependencies {
    api 'org.junit.platform:junit-platform-engine:1.10.3'
    api 'org.junit.platform:junit-platform-commons:1.10.3'
    api 'org.seleniumhq.selenium:selenium-java:4.22.0'
    examplesImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.3'
    examplesImplementation 'io.github.bonigarcia:webdrivermanager:5.9.1'
    examplesImplementation 'io.undertow:undertow-core:2.3.14.Final'
    examplesImplementation 'ch.qos.logback:logback-classic:1.5.6'
    examplesImplementation 'org.slf4j:jul-to-slf4j:2.0.13'
    examplesImplementation 'net.amygdalum:fluent-allotropy:0.0.2'  
}

group = 'net.amygdalum'
version = '0.0.3-SNAPSHOT'
description = 'net.amygdalum:allotropy'
java.sourceCompatibility = JavaVersion.VERSION_17


task javadocJar(type: Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

jar {
    manifest {
        attributes(
            'Automatic-Module-Name': 'net.amygdalum.allotropy'
        )
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            artifact sourcesJar
            artifact javadocJar
            
	        pom {
		        name = 'Allotropy'
		        description = 'A Test Framework for testing Responsive Layouts'
		        url = 'https://github.com/almondtools/allotropy'
	            licenses {
	                license {
			            name = 'GNU LESSER GENERAL PUBLIC LICENSE (LGPL)'
	        		    url = 'http://www.gnu.org/licenses/lgpl.txt'
	                }
	            }
	            developers {
	                developer {
			            id = 'amygdalum'
	        		    name = 'Stefan Mandel'
			            email = 'mail@amygdalum.net'
	                }
	            }
	            scm {
					connection = 'scm:git:git@github.com:almondtools/allotropy.git'
					developerConnection = 'scm:git:git@github.com:almondtools/allotropy.git'
					url = 'git@github.com:almondtools/allotropy.git'
	            }
	        }
        }
    }
	repositories {
        maven {
            name = 'sonatype'
            credentials(PasswordCredentials)
        }
    }
}

signing {
	sign publishing.publications.mavenJava
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    options.addBooleanOption('Xdoclint:none', true)
}
