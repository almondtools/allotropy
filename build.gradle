plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0-rc-1"
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

configurations {
    examplesImplementation.extendsFrom testImplementation
}

sourceSets {
    examples {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/examples/java')
        }
        resources.srcDir file('src/examples/resources')
    }
}

dependencies {
    api 'org.junit.platform:junit-platform-engine:1.10.0'
    api 'org.junit.platform:junit-platform-commons:1.10.0'
    api 'org.seleniumhq.selenium:selenium-java:4.10.0'
    examplesImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    examplesImplementation 'io.github.bonigarcia:webdrivermanager:5.4.1'
    examplesImplementation 'io.undertow:undertow-core:2.3.7.Final'
    examplesImplementation 'ch.qos.logback:logback-classic:1.4.8'
    examplesImplementation 'org.slf4j:jul-to-slf4j:2.0.7'
    
}

group = 'net.amygdalum'
version = '0.0.1-SNAPSHOT'
description = 'net.amygdalum:allotropy'
java.sourceCompatibility = JavaVersion.VERSION_20

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            
	        pom {
		        name = 'Allotropy'
		        description = 'A Test Framework for testing Responsive Layouts'
		        url = 'https://github.com/almondtools/allotropy'
	            licenses {
	                license {
			            name = 'GNU LESSER GENERAL PUBLIC LICENSE (LGPL)'
	        		    url = 'http://www.gnu.org/licenses/lgpl.txt'
	                }
	            }
	            developers {
	                developer {
			            id = 'amygdalum'
	        		    name = 'Stefan Mandel'
			            email = 'mail@amygdalum.net'
	                }
	            }
	            scm {
					connection = 'scm:git:git@github.com:almondtools/allotropy.git'
					developerConnection = 'scm:git:git@github.com:almondtools/allotropy.git'
					url = 'git@github.com:almondtools/allotropy.git'
	            }
	        }
        }
    }
}

signing {
	sign publishing.publications.mavenJava
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}


